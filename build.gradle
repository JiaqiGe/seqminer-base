apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'scala'

//sourceCompatibility = 1.7
//targetCompatibility = 1.7

configurations {
  compile.exclude group: 'org.eclipse.jetty.orbit'
  awsSparkJar { transitive = false }
}

buildscript {
  repositories {
    maven { url = "http://repository.orbitz.net/repo" }
    mavenCentral()
  }
}

repositories {
  maven { url = "http://repository.orbitz.net/repo" }
  mavenCentral()
}

dependencies {
  compile 'org.apache.spark:spark-sql_2.10:1.3.1'
  compile 'org.apache.spark:spark-streaming_2.10:1.3.1'
  compile 'joda-time:joda-time:2.7'
  compile 'org.apache.spark:spark-streaming-kinesis-asl_2.10:1.3.1'
  compile 'org.elasticsearch:elasticsearch-spark_2.10:2.1.0.Beta3'
  compile 'log4j:log4j:1.2.17'
  compile 'com.datastax.spark:spark-cassandra-connector_2.10:1.2.1'
  compile 'com.datastax.spark:spark-cassandra-connector-java_2.10:1.2.1'
  compile 'com.google.code.gson:gson:2.3.1'
  compile 'com.opencsv:opencsv:3.3'

  awsSparkJar 'com.amazonaws:amazon-kinesis-client:1.1.0'
  awsSparkJar 'com.amazonaws:aws-java-sdk:1.8.3'
  awsSparkJar 'org.elasticsearch:elasticsearch-spark_2.10:2.1.0.Beta3'
  awsSparkJar 'com.google.guava:guava:14.0.1'
  awsSparkJar 'joda-time:joda-time:2.7'
  awsSparkJar 'org.apache.spark:spark-streaming-kinesis-asl_2.10:1.3.1'
  awsSparkJar 'com.datastax.spark:spark-cassandra-connector_2.10:1.2.1'
  awsSparkJar 'com.datastax.spark:spark-cassandra-connector-java_2.10:1.2.1'
  awsSparkJar 'org.apache.spark:spark-sql_2.10:1.3.1'
  awsSparkJar 'com.google.code.gson:gson:2.3.1'

  testCompile 'junit:junit:4.8.2'
  testCompile 'org.apache.mrunit:mrunit:1.0.0:hadoop2'
  testCompile 'org.easytesting:fest-assert-core:2.0M10'
  testCompile 'org.scalatest:scalatest_2.10:3.0.0-M3'
}

task copySparkJar(type: Copy) {
  into 'build/hds-app-java/lib'
  from configurations.awsSparkJar
}

task copyJar(type: Copy, dependsOn: jar) {
  into 'build/hds-app-java'
  from jar.archivePath
}

task copyScript(type: Copy) {
  into 'build/hds-app-java'
  from 'src/main/scripts'
}

task deployToAws (type: Exec, dependsOn: [copySparkJar, copyJar, copyScript]) {
  commandLine 'rsync'
  args = ['-avz', '--delete', 'build/hds-app-java/', 'aws-spark:~/hds-app-java/']
}

task deployToHadoop (type: Exec, dependsOn: [copySparkJar, copyJar, copyScript]) {
  commandLine 'rsync'
  args = ['-avz', '--delete', 'build/hds-app-java/', 'orbitz-spark:~/hds-app-java/']
}

task deployToLocal (dependsOn: [copySparkJar, copyJar, copyScript]) {
  doLast {
    println "\n============================"
    println "Built successfully."
    println "Check 'build/hds-app-java' directory."
    println "============================"
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}


